c-----------------------------------------------------------------------
      subroutine geom_avg_wrapper(u,gs_hndl,nelx,nely,nelz,ifld,idir
     $     ,nrepeat)
      include 'SIZE'
      include 'TOTAL'
      real u(lx1*ly1*lz1*lelt)
      real w1(lx1*ly1*lz1*lelt)
      integer nt

      nt = lx1*ly1*lz1*nelt

      call geom_avg_tensor(w1,u,gs_hndl,nelx,nely,nelz,ifld,idir
     $     ,nrepeat)

      call rzero(u,nt)

      do i=1,nt
         u(i) = w1(i)
      enddo

      return
      end
c-----------------------------------------------------------------------
      subroutine geom_avg_tensor(ua,u,gs_hndl,nelx,nely,nelz,ifld,idir
     $     ,nrepeat)
      include 'SIZE'
      include 'TOTAL'
      real u (lx1,ly1,lz1,lelt)
      real ua(lx1,ly1,lz1,lelt)
      integer gs_hndl,e,ex,ey,ez,eg
      integer nrepeat
      real onebyn

      if(nrepeat.eq.0) call exitt

      if (gs_hndl.eq.0) then
       if (idir.eq.1)
     $       call set_gs_xgeom_hndl(gs_hndl,nelx,nely,nelz,ifld,nrepeat)
       if (idir.eq.2)
     $       call set_gs_ygeom_hndl(gs_hndl,nelx,nely,nelz,ifld,nrepeat)
       if (idir.eq.3)
     $       call set_gs_zgeom_hndl(gs_hndl,nelx,nely,nelz,ifld,nrepeat)
      endif

      nel = nelfld(ifld)
      n   = nx1*ny1*nz1*nel

      call copy(ua,bm1,n)                 ! Set the averaging weights
      call fgslib_gs_op(gs_hndl,ua,1,1,0) ! Sum weights over columns

      do i=1,n                          ! ua = (w_j*u_j)/( sum_i w_i)
         ua(i,1,1,1) = bm1(i,1,1,1)*u(i,1,1,1)/ua(i,1,1,1)
      enddo

      call fgslib_gs_op(gs_hndl,ua,1,1,0) ! Sum weighted values

c     onebyn = 1/real(nrepeat)
c     call copy(ua,u,n)
c     call fgslib_gs_op(gs_hndl,ua,1,1,0)
c     if(nid.eq.0) write(6,*) "1/nrepeat is", onebyn 
c     do i=1,n
c        ua(i,1,1,1) = ua(i,1,1,1)*onebyn
c     enddo

      return
      end
c-----------------------------------------------------------------------
      subroutine set_gs_xgeom_hndl(gs_hndl,nelx,nely,nelz,ifld,nrepeat)
c     Set the x-average handle
      include 'SIZE'
      include 'TOTAL'
      integer gs_avg_hndl,e,ex,ey,ez,eg,nelx,nely,nelz
      common /nekmpi/ mid,mp,nekcomm,nekgroup,nekreal
      common /c_is1/ glo_num(lx1,ly1,lz1,lelv)

      integer*8 glo_num
      integer*8 e_num,lx,ly,lpl,lb4
      integer erepeat

      nel = nelfld(ifld)
      erepeat = int(nelx/nrepeat)

      lx  = nelx*(nx1-1)+1
      ly  = nely*(ny1-1)+1
      lz  = nelz*(nz1-1)+1
      lpl = ly*lz

      do e=1,nel
       eg = lglel(e)
       call get_exyz(ex,ey,ez,eg,nelx,nely,nelz)
       e_num = mod1(ex,erepeat) 
       do k=1,nz1      ! Enumerate points in the x-y plane
       do j=1,ny1
       do i=1,nx1
          lb4=      lpl*(i+(e_num-1)*(nx1-1)-1) 
          lb4= lb4 + ly*(k+(ez   -1)*(nz1-1)-1)
          glo_num(i,j,k,e)=lb4+j+(ey-1)*(ny1-1) 
       enddo
       enddo
       enddo
      enddo

      n = nel*nx1*ny1*nz1
      call fgslib_gs_setup(gs_hndl,glo_num,n,nekcomm,mp)

      return
      end
c-----------------------------------------------------------------------
      subroutine set_gs_ygeom_hndl(gs_hndl,nelx,nely,nelz,ifld,nrepeat)
c     Set the y-average handle
      include 'SIZE'
      include 'TOTAL'
      integer gs_avg_hndl,e,ex,ey,ez,eg,nelx,nely,nelz
      common /nekmpi/ mid,mp,nekcomm,nekgroup,nekreal
      common /c_is1/ glo_num(lx1,ly1,lz1,lelv)

      integer*8 glo_num
      integer*8 e_num,lx,ly,lpl,lb4
      integer erepeat

      nel = nelfld(ifld)
      erepeat = int(nely/nrepeat)

      lx  = nelx*(nx1-1)+1
      ly  = nely*(ny1-1)+1
      lz  = nelz*(nz1-1)+1
      lpl = lx*lz

      do e=1,nel
       eg = lglel(e)
       call get_exyz(ex,ey,ez,eg,nelx,nely,nelz)
       e_num = mod1(ey,erepeat) 
       do k=1,nz1      ! Enumerate points in the x-y plane
       do j=1,ny1
       do i=1,nx1
          lb4=      lpl*(j+(e_num-1)*(ny1-1)-1) 
          lb4= lb4 + lx*(k+(ez   -1)*(nz1-1)-1)
          glo_num(i,j,k,e)=lb4+i+(ex-1)*(nx1-1) 
       enddo
       enddo
       enddo
      enddo

      n = nel*nx1*ny1*nz1
      call fgslib_gs_setup(gs_hndl,glo_num,n,nekcomm,mp)

      return
      end
c-----------------------------------------------------------------------
      subroutine set_gs_zgeom_hndl(gs_avg_hndl,nelx,nely,nelz,ifld
     $     ,nrepeat)
c     Set the z-average handle
      include 'SIZE'
      include 'TOTAL'
      integer gs_avg_hndl,e,ex,ey,ez,eg,nelx,nely,nelz
      common /nekmpi/ mid,mp,nekcomm,nekgroup,nekreal
      common /c_is1/ glo_num(lx1,ly1,lz1,lelv)

      integer*8 glo_num
      integer*8 e_num,lx,ly,lpl,lb4
      integer erepeat

      nel = nelfld(ifld)
      erepeat = int(nelz/nrepeat)

      lx  = nelx*(nx1-1)+1
      ly  = nely*(ny1-1)+1
      lz  = nelz*(nz1-1)+1
      lpl = lx*ly

      do e=1,nel
       eg = lglel(e)
       call get_exyz(ex,ey,ez,eg,nelx,nely,nelz)
       e_num = mod1(ez,erepeat) 
       do k=1,nz1      ! Enumerate points in the x-y plane
       do j=1,ny1
       do i=1,nx1
          lb4=      lpl*(k+(e_num-1)*(nz1-1)-1) 
          lb4= lb4 + lx*(j+(ey   -1)*(ny1-1)-1)
          glo_num(i,j,k,e)=lb4+i+(ex-1)*(nx1-1) 
       enddo
       enddo
       enddo
      enddo

      n = nel*nx1*ny1*nz1
      call fgslib_gs_setup(gs_avg_hndl,glo_num,n,nekcomm,mp)

      return
      end
c-----------------------------------------------------------------------
